#!/usr/bin/perl -T
# vim: set filetype=perl shiftwidth=2 tabstop=2 expandtab :
use strict;
use warnings;
$|++;

use CGI;
use CGI::Carp qw(fatalsToBrowser);
use HTML::Template;

my $root_path       = '/var/www/projects/flustrw';
my $local_data_path = '/var/www/data/flustrw';

my $tmpl_path       = $root_path.'/tmpl/flustrw.tmpl';
my $bin_path        = $root_path.'/bin';
my $script          = $bin_path.'/flustrw.r';

my $fred_root        = '/mnt/fred/shougroup';
my $remote_data_path = $fred_root.'/Honeycomb/data/flustrw';
my $result_folder_name = '-flustr_results';

#my $home_url = "http://honeycomb.fhcrc.org/cgi-bin";
my $home_url = "http://localhost/cgi-bin";

$ENV{"PATH"}="/bin:/usr/bin";

my $q = CGI->new();
my $t = HTML::Template->new(
                            filename  => $tmpl_path,
                            debug     => '0',
                            associate => $q,
                           );

my $p = get_params($q, $t);
# User forgot initials.
if ($p->{data_folder} and not $p->{user}) {
  $t->param(user_error => "Need user's initials!");
  print_page($t);

# We're starting fresh.  Show a blank (except for defaults) form.
} elsif (!$p->{user}) {
  print_page($t);

} elsif ($p->{waiting}) {
  warn "WAITING";
  my $local_result_path  = $local_data_path.'/'.
    $p->{user}.'/'.$p->{timestamp}.$result_folder_name;

  my $runlog_path = $local_result_path.'/'.$p->{timestamp}.'-run.log';
  my $runlog_path_final = $runlog_path.'-final';

  if (-e $runlog_path_final) {
    open my $rl, $runlog_path_final
      or die "Couldn't open final runlog for reading!:$!";
    my $runlog = do { local $/; <$rl> };

    my $res_path = '//fred/shougroup/Honeycomb/data/flustrw/'.
                   $p->{user}.'/'.$p->{timestamp}.$result_folder_name;

    $t->param(runlog  => $runlog,
              waiting => '0');

    if (grep /DONE!/, $runlog and not grep /ERROR/, $runlog) {
      $t->param(success => '1',
                result_path => $res_path);
    } else {
      $t->param(error => '1')
    }
    print_page($t);
  } elsif (-e $runlog_path) {
    open my $rl, $runlog_path 
      or die "Couldn't open runlog for reading!:$!";
    my $runlog = do { local $/; <$rl> };
    $t->param(runlog => $runlog);
    close $rl;
    print_page($t);
  } else {
    print_page($t);
    croak "Couldn't find runlog '".$runlog_path."'!";
  }

# Check the form values and run the code!
} elsif ($p->{data_folder} and $p->{user}) {
  my $data_folder = check_data_path($p->{data_folder},$t);
  my $input_data_path = $fred_root.'/'.$data_folder;
  check_input_data($input_data_path);


  my $timestamp = make_ts();
  my $local_user_path   = $local_data_path.'/'.$p->{user};
  my $local_result_path = $local_user_path.'/'.
                          $timestamp.$result_folder_name;
  my $remote_result_path = $remote_data_path.'/'.$p->{user};
  my $r    = 'R --vanilla --slave';
  my $args = join " ", ($input_data_path,
                        $local_user_path,
                        $timestamp, 
                        $p->{beads} ? 'TRUE' : 'FALSE',
                        $p->{bead_dil},
                        $p->{beads_per_ul},
                        $p->{f_channels},
                        $p->{fsc_sig},
                        $p->{dead_sig},
                        $p->{cluster_sig},
                        $p->{all_bright},
                        $p->{all_dim},
                       );
  my $runlog_path = $local_result_path.'/'.$timestamp.'-run.log';

  my $run_command = join " ", ($r, "<", $script, "--args", $args);

  system("mkdir -p $local_result_path") == 0
    or croak "Couldn't make local data directory: $!";
  system("chmod a+w $local_result_path") == 0
    or croak "Couldn't chmod local data directory: $!:";

  open my $fh, ">$runlog_path" 
    or die "Couldn't open $runlog_path for writing: $!";

  my $pid = fork;
  croak "Cannot fork: $!" unless defined $pid;
  if ($pid) { # parent
    $q->param('waiting'     ,'1');
    $q->param('timestamp'   , $timestamp);
    $q->param('beads'       , $p->{beads});
    $q->param('bead_dil'    , $p->{bead_dil});
    $q->param('bead_conc'   , $p->{beads_per_ul});
    $q->param('channels'    , @{$p->{channels}});
    $q->param('fsc_sig'     , $p->{fsc_sig});
    $q->param('dead_sig'    , $p->{dead_sig});
    $q->param('cluster_sig' , $p->{cluster_sig});
    $q->param('all_bright'  , $p->{all_bright});
    $q->param('all_dim'     , $p->{all_dim});
    print $q->redirect();
  } else { # child
    close STDOUT;
    close STDIN;
    unless (open F, "-|") {
      open STDIN, "</dev/null";
      open STDOUT, ">/dev/null";
      open STDERR, ">&=1";
      exec $run_command or die "Cannot execute script: $!";
    }
    while (<F>) { 
      open my $fh, '>>', $runlog_path 
        or die "Couldn't open $runlog_path to append!: $!";
      print $fh $_;
      close $fh;
    }
    close F;
    system("mv $runlog_path $runlog_path-final");
    system("cp -r $local_result_path $remote_result_path") == 0
        or croak "Couldn't copy from ".
                 $local_result_path." to ".$remote_result_path.": $!";

    exit 0;
  }
} else {
  warn "DIDN'T MATCH ANYTHING!";
  print_page($t);
}

sub get_params {
  my $q = shift;
  my $t = shift;
  my ($user)         = $q->param('user')        =~ /^(\w+)$/;
  my ($timestamp)    = $q->param('timestamp')   =~ /^(\d+_\d+)$/;

  my ($beads)        = $q->param('beads')       =~ /^(\w+)$/;
  my ($beads_per_ul) = $q->param('bead_conc')   =~ /^(\d+(\.\d+)?)$/;
  my ($bead_dil)     = $q->param('bead_dil')    =~ /^(\d+)$/;

  my ($all_bright)   = $q->param('all_bright')  =~ /^(\d+(\.\d+)?)$/;
  my ($all_dim)      = $q->param('all_dim')     =~ /^(\d+(\.\d+)?)$/;

  my ($cluster_sig)  = $q->param('cluster_sig') =~ /^(\d+(\.\d+)?)$/;
  my ($fsc_sig)      = $q->param('fsc_sig')     =~ /^(\d+(\.\d+)?)$/;
  my ($dead_sig)     = $q->param('dead_sig')    =~ /^(\d+(\.\d+)?)$/;

  my ($data_folder)  = $q->param('data_folder') =~ /^([\s\/\\\w-]+)$/; 
  my ($result)       = $q->param('result')      =~ /^(\d)$/;
  my ($waiting)      = $q->param('waiting')     =~ /^(\d)$/;
  my @channels       = $q->param('channels');
  
  my $f_channels = prepare_channels(\@channels,$q);

  $user = lc($user) if $user;
  my $default_conc_ul      = 7.8e3;
  my $default_bead_dil     = 10;
  my $default_fsc_sig      = 0.5;
  my $default_dead_sig     = 1;
  my $default_cluster_sig  = 1;
  my $default_all_bright   = 4;
  my $default_all_dim      = 2;

  $q->param('def_bead_conc' => $default_conc_ul);
  $q->param('def_bead_dil'  => $default_bead_dil);

  $q->param('def_fsc_sig'      => $default_fsc_sig);
  $q->param('def_dead_sig'     => $default_dead_sig);
  $q->param('def_cluster_sig'  => $default_cluster_sig);

  $q->param('def_all_bright' => $default_all_bright);
  $q->param('def_all_dim'    => $default_all_dim);

  return  { user         => $user,
            f_channels   => $f_channels,
            channels     => \@channels,
            timestamp    => $timestamp,
            beads        => $beads,
            beads_per_ul => $beads_per_ul,
            bead_dil     => $bead_dil,
            data_folder  => $data_folder,
            result       => $result,
            waiting      => $waiting,
            fsc_sig      => $fsc_sig,
            dead_sig     => $dead_sig,
            cluster_sig  => $cluster_sig,
            all_bright   => $all_bright,
            all_dim      => $all_dim,
          }
}

sub check_data_path {
  my $folder = shift;
  my $t = shift;
  $folder =~ s/\\/\//g;
  if ($folder =~ /\s+/) {
    $t->param(folder_error => "Folder name ('$folder') contains spaces!");
    print_page($t);
  } elsif (not defined $folder) {
    $t->param(folder_error => "Folder name contains funny characters!");
    print_page($t);
  }
  return $folder;
}

sub prepare_channels {
  my $chans = shift;
  my $q     = shift;
  my $channels = "";
  foreach (@$chans) {
    /^(\w+)$/;
    $channels .= $1.',';
    $q->param($1 => '1');
  }
  chop $channels;
  return $channels;
}

sub check_input_data {
  my $path = shift;
  if (-e $path) {
    check_files($path);
  } else {
    $t->param(folder_error => "Path '$path'  doesn't exist!");
    print_page($t);
  }
}


sub check_files {
  my $dir = shift;
  opendir DIR, $dir or croak "Can't open dir '$dir': $!";
  my @fs = readdir DIR;
  @fs = grep{ !-d && /\.fcs$/ } @fs;
  closedir DIR;

  unless (@fs) {
    $t->param(folder_error => "No '.fcs' files in folder '$dir'!");
    print_page($t);
  }

  if (grep{ /\s+/ } @fs) {
    $t->param(folder_error => "File name(s) contain spaces!");
    print_page($t);
  }
}

sub make_ts {
  my ($sec, $min, $hour, $mday, $mon, $year, $wday, $yday, $isdst) = 
    localtime(time);

  foreach (\$min, \$hour, \$mday, \{++$mon}) {
    $$_ = '0'.$$_ if  $$_ < 10;
  }
  return ($year+1900).$mon.$mday.'_'.$hour.$min;
}

sub print_page {
  my $t_obj = shift;
  print $q->header();
  print $t_obj->output();
  exit;
}
